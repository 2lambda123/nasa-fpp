(* ----------------------------------------------------------------------
 * fpp.grm
 * ----------------------------------------------------------------------*) 

open Ast
open AstNode
open Loc

fun pos1 node = 
  let val Loc {pos1,...} = Locations.lookup node
  in pos1 end

fun pos2 node = 
  let val Loc {pos2,...} = Locations.lookup node
  in pos2 end

fun node (e, pos1, pos2) =
let
  val node = AstNode.node e
in
  Locations.insert (node, Loc {
    file=(!ParserState.file),
    pos1=pos1,
    pos2=pos2
  });
  node
end

%%

%pos int

%eop EOF

%term 
  ACTIVE
| ARRAY
| ASSERT
| ASYNC
| AT
| BLOCK
| BOOL
| COLON
| COMMA
| COMMAND
| COMPONENT
| CONSTANT
| DEFAULT
| DOT
| DROP
| ENUM
| EOF
| EOL
| EQUALS
| EVENT
| F32
| F64
| FALSE
| GET
| GUARDED
| I16
| I32
| I64
| I8
| IDENT of string
| INPUT
| INSTANCE
| INTERNAL
| LBRACE
| LBRACKET
| LITERAL_BOOL of string
| LITERAL_FLOAT of string
| LITERAL_INT of string
| LITERAL_STRING of string
| LOCATE
| LPAREN
| MINUS
| MODULE
| OUTPUT
| PARAM
| PASSIVE
| PLUS
| PORT
| POST_ANNOTATION of string
| PRE_ANNOTATION of string
| QUEUED
| RARROW
| RBRACE
| RBRACKET
| REF
| REG
| RESP
| RPAREN
| SEMI
| SET
| SIZE
| SLASH
| STAR
| STRING
| STRUCT
| SYNC
| TELEMETRY
| TIME
| TOPOLOGY
| TRUE
| TYPE
| U16
| U32
| U64
| U8
(* The following terminals exist for precedence rules only *)
| UNARY_MINUS

%left PLUS MINUS
%left STAR SLASH 
%nonassoc UNARY_MINUS
%nonassoc DOT

%nonterm 
  START of trans_unit
| ARRAY_CELL of expr node
| ARRAY_CELL_LIST of expr node list
| COMMA_OR_EOL of unit
| COMPONENT_KIND of component_kind
| COMPONENT_MEMBER of component_member
| COMPONENT_MEMBER_LIST of component_member list
| COMPONENT_MEMBER_NODE of component_member_node
| DEF_ABS_TYPE of def_abs_type
| DEF_ABS_TYPE_NODE of def_abs_type node
| DEF_ARRAY of def_array
| DEF_ARRAY_NODE of def_array node
| DEF_COMPONENT of def_component
| DEF_COMPONENT_NODE of def_component node
| DEF_CONSTANT of def_constant
| DEF_CONSTANT_NODE of def_constant node
| DEF_ENUM of def_enum
| DEF_ENUM_CONSTANT of def_enum_constant
| DEF_ENUM_CONSTANT_NODE of def_enum_constant node
| DEF_ENUM_CONSTANT_NODE_ANNOTATED of def_enum_constant node annotated
| DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST of def_enum_constant node annotated list
| DEF_ENUM_NODE of def_enum node
| DEF_MODULE of def_module
| DEF_MODULE_NODE of def_module node
| DEF_PORT of def_port
| DEF_PORT_NODE of def_port node
| DEF_STRUCT of def_struct
| DEF_STRUCT_NODE of def_struct node
| EOL_LBRACE_EOL of unit
| EXPR of expr
| EXPR_NODE of expr node
| FORMAL_PARAM of formal_param
| FORMAL_PARAM_NODE of formal_param node
| FORMAL_PARAM_NODE_ANNOTATED of formal_param node annotated
| FORMAL_PARAM_NODE_ANNOTATED_LIST of formal_param node annotated list
| LAST_COMPONENT_MEMBER of component_member
| LAST_COMPONENT_MEMBER_NODE of component_member_node
| LAST_DEF_ENUM_CONSTANT of def_enum_constant
| LAST_DEF_ENUM_CONSTANT_NODE of def_enum_constant node
| LAST_DEF_ENUM_CONSTANT_NODE_ANNOTATED of def_enum_constant node annotated
| LAST_FORMAL_PARAM of formal_param
| LAST_FORMAL_PARAM_NODE of formal_param node
| LAST_FORMAL_PARAM_NODE_ANNOTATED of formal_param node annotated
| LAST_STRUCT_MEMBER of struct_member
| LAST_STRUCT_TYPE_MEMBER of struct_type_member
| LAST_STRUCT_TYPE_MEMBER_NODE of struct_type_member node
| LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED of struct_type_member node annotated
| LAST_TU_MEMBER of tu_member
| LAST_TU_MEMBER_NODE of tu_member_node
| POST_ANNOTATION_LIST of string list
| PRE_ANNOTATION_LIST of string list
| QUAL_IDENT of ident list
| SEMI_OR_EOL of unit
| SPEC_LOC of spec_loc
| SPEC_LOC_NODE of spec_loc node
| STRUCT_MEMBER of struct_member
| STRUCT_MEMBER_LIST of struct_member list
| STRUCT_TYPE_MEMBER of struct_type_member
| STRUCT_TYPE_MEMBER_NODE of struct_type_member node
| STRUCT_TYPE_MEMBER_NODE_ANNOTATED of struct_type_member node annotated
| STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST of struct_type_member node annotated list
| TRANS_UNIT of trans_unit
| TU_MEMBER of tu_member
| TU_MEMBER_LIST of tu_member list
| TU_MEMBER_NODE of tu_member_node
| TYPE_NAME of type_name
| TYPE_NAME_FLOAT of type_name
| TYPE_NAME_IDENT of type_name
| TYPE_NAME_INT of type_name
| TYPE_NAME_NODE of type_name node

%name FPP

%noshift EOF
%nodefault
%%

START 
  : TRANS_UNIT (TRANS_UNIT)

ARRAY_CELL
  : EXPR_NODE COMMA_OR_EOL (EXPR_NODE)

ARRAY_CELL_LIST
  : EXPR_NODE ([ EXPR_NODE ])
  | ARRAY_CELL ARRAY_CELL_LIST (ARRAY_CELL :: ARRAY_CELL_LIST)

COMMA_OR_EOL
  : COMMA (())
  | EOL (())

COMPONENT_KIND
  : ACTIVE (ComponentActive)
  | PASSIVE (ComponentPassive)
  | QUEUED (ComponentQueued)

COMPONENT_MEMBER
  : COMPONENT_MEMBER_NODE (ComponentMember ([], COMPONENT_MEMBER_NODE, [])) 
  | PRE_ANNOTATION_LIST COMPONENT_MEMBER_NODE (
      ComponentMember (PRE_ANNOTATION_LIST, COMPONENT_MEMBER_NODE, [])
    ) 
  | COMPONENT_MEMBER_NODE POST_ANNOTATION_LIST (
      ComponentMember ([], COMPONENT_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_COMPONENT_MEMBER_NODE POST_ANNOTATION_LIST (
      ComponentMember ([], LAST_COMPONENT_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST COMPONENT_MEMBER_NODE POST_ANNOTATION_LIST (
      ComponentMember (PRE_ANNOTATION_LIST, COMPONENT_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_COMPONENT_MEMBER_NODE POST_ANNOTATION_LIST (
      ComponentMember (PRE_ANNOTATION_LIST, LAST_COMPONENT_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

COMPONENT_MEMBER_LIST
  : LAST_COMPONENT_MEMBER ([ LAST_COMPONENT_MEMBER ])
  | COMPONENT_MEMBER ([ COMPONENT_MEMBER ])
  | COMPONENT_MEMBER COMPONENT_MEMBER_LIST (COMPONENT_MEMBER :: COMPONENT_MEMBER_LIST)

COMPONENT_MEMBER_NODE
  : LAST_COMPONENT_MEMBER_NODE SEMI_OR_EOL (LAST_COMPONENT_MEMBER_NODE)

DEF_ABS_TYPE
  : TYPE IDENT (DefAbsType IDENT)

DEF_ABS_TYPE_NODE
  : DEF_ABS_TYPE (node (DEF_ABS_TYPE, DEF_ABS_TYPEleft, DEF_ABS_TYPEright))

DEF_ARRAY
  : ARRAY IDENT EQUALS LBRACKET EXPR_NODE RBRACKET TYPE_NAME_NODE (
      DefArray (IDENT, EXPR_NODE, TYPE_NAME_NODE, NONE)
    )
  | ARRAY IDENT EQUALS LBRACKET EXPR_NODE RBRACKET TYPE_NAME_NODE DEFAULT EXPR_NODE (
      DefArray (IDENT, EXPR_NODE1, TYPE_NAME_NODE, SOME EXPR_NODE2)
    )

DEF_ARRAY_NODE
  : DEF_ARRAY (node (DEF_ARRAY, DEF_ARRAYleft, DEF_ARRAYright))

DEF_COMPONENT
  : COMPONENT_KIND COMPONENT IDENT LBRACE RBRACE (
      DefComponent (COMPONENT_KIND, IDENT, [])
    )
  | COMPONENT_KIND COMPONENT IDENT LBRACE COMPONENT_MEMBER_LIST RBRACE (
      DefComponent (COMPONENT_KIND, IDENT, COMPONENT_MEMBER_LIST)
    )

DEF_COMPONENT_NODE
  : DEF_COMPONENT (node (DEF_COMPONENT, DEF_COMPONENTleft, DEF_COMPONENTright))

DEF_CONSTANT
  : CONSTANT IDENT EQUALS EXPR_NODE (DefConstant (IDENT, EXPR_NODE))

DEF_CONSTANT_NODE
  : DEF_CONSTANT (node (DEF_CONSTANT, DEF_CONSTANTleft, DEF_CONSTANTright))

DEF_ENUM
  : ENUM IDENT LBRACE RBRACE (
      DefEnum (IDENT, NONE, [])
    )
  | ENUM IDENT LBRACE DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST RBRACE (
      DefEnum (IDENT, NONE, DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST)
    )
  | ENUM IDENT COLON TYPE_NAME_NODE LBRACE DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST RBRACE (
      DefEnum (IDENT, SOME TYPE_NAME_NODE, DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST)
    )
  | ENUM IDENT COLON TYPE_NAME_NODE LBRACE RBRACE (
      DefEnum (IDENT, SOME TYPE_NAME_NODE, [])
    )

DEF_ENUM_CONSTANT
  : LAST_DEF_ENUM_CONSTANT COMMA_OR_EOL (LAST_DEF_ENUM_CONSTANT)

DEF_ENUM_CONSTANT_NODE
  : DEF_ENUM_CONSTANT (node (DEF_ENUM_CONSTANT, DEF_ENUM_CONSTANTleft, DEF_ENUM_CONSTANTright))

DEF_ENUM_CONSTANT_NODE_ANNOTATED
  : DEF_ENUM_CONSTANT_NODE (([], DEF_ENUM_CONSTANT_NODE, [])) 
  | PRE_ANNOTATION_LIST DEF_ENUM_CONSTANT_NODE (
      (PRE_ANNOTATION_LIST, DEF_ENUM_CONSTANT_NODE, [])
    ) 
  | DEF_ENUM_CONSTANT_NODE POST_ANNOTATION_LIST (
      ([], DEF_ENUM_CONSTANT_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_DEF_ENUM_CONSTANT_NODE POST_ANNOTATION_LIST (
      ([], LAST_DEF_ENUM_CONSTANT_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST DEF_ENUM_CONSTANT_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, DEF_ENUM_CONSTANT_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_DEF_ENUM_CONSTANT_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, LAST_DEF_ENUM_CONSTANT_NODE, POST_ANNOTATION_LIST)
    ) 

DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST
  : LAST_DEF_ENUM_CONSTANT_NODE_ANNOTATED ([ LAST_DEF_ENUM_CONSTANT_NODE_ANNOTATED ])
  | DEF_ENUM_CONSTANT_NODE_ANNOTATED ([ DEF_ENUM_CONSTANT_NODE_ANNOTATED ])
  | DEF_ENUM_CONSTANT_NODE_ANNOTATED DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST (
      DEF_ENUM_CONSTANT_NODE_ANNOTATED :: DEF_ENUM_CONSTANT_NODE_ANNOTATED_LIST
    )

DEF_ENUM_NODE
  : DEF_ENUM (node (DEF_ENUM, DEF_ENUMleft, DEF_ENUMright))

DEF_MODULE
  : MODULE IDENT LBRACE RBRACE (DefModule (IDENT, []))
  | MODULE IDENT LBRACE TU_MEMBER_LIST RBRACE (
      DefModule (IDENT, TU_MEMBER_LIST)
    )

DEF_MODULE_NODE
  : DEF_MODULE (node (DEF_MODULE, DEF_MODULEleft, DEF_MODULEright))

DEF_PORT
  : PORT IDENT LPAREN RPAREN (
      DefPort (IDENT, [], NONE)
    )
  | PORT IDENT LPAREN RPAREN RARROW TYPE_NAME_NODE (
      DefPort (IDENT, [], SOME TYPE_NAME_NODE)
    )
  | PORT IDENT LPAREN FORMAL_PARAM_NODE_ANNOTATED_LIST RPAREN (
      DefPort (IDENT, FORMAL_PARAM_NODE_ANNOTATED_LIST, NONE)
    )
  | PORT IDENT LPAREN FORMAL_PARAM_NODE_ANNOTATED_LIST RPAREN RARROW TYPE_NAME_NODE (
      DefPort (IDENT, FORMAL_PARAM_NODE_ANNOTATED_LIST, SOME TYPE_NAME_NODE)
    )

DEF_PORT_NODE
  : DEF_PORT (node (DEF_PORT, DEF_PORTleft, DEF_PORTright))

DEF_STRUCT
  : STRUCT IDENT LBRACE STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST RBRACE (
      DefStruct (IDENT, STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST, NONE)
    )
  | STRUCT IDENT LBRACE STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST RBRACE DEFAULT EXPR_NODE (
      DefStruct (IDENT, STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST, SOME EXPR_NODE)
    )

DEF_STRUCT_NODE
  : DEF_STRUCT (node (DEF_STRUCT, DEF_STRUCTleft, DEF_STRUCTright))

EXPR
  : EXPR_NODE DOT IDENT %prec DOT (ExprDot (EXPR_NODE, IDENT))
  | EXPR_NODE MINUS EXPR_NODE %prec PLUS (ExprBinop (EXPR_NODE1, Sub, EXPR_NODE2))
  | EXPR_NODE PLUS EXPR_NODE %prec MINUS (ExprBinop (EXPR_NODE1, Add, EXPR_NODE2))
  | EXPR_NODE SLASH EXPR_NODE %prec SLASH (ExprBinop (EXPR_NODE1, Div, EXPR_NODE2))
  | EXPR_NODE STAR EXPR_NODE %prec STAR (ExprBinop (EXPR_NODE1, Mul, EXPR_NODE2))
  | FALSE (ExprLiteralBool (False))
  | IDENT (ExprIdent IDENT)
  | LBRACE STRUCT_MEMBER_LIST RBRACE (ExprStruct (STRUCT_MEMBER_LIST))
  | LBRACKET ARRAY_CELL_LIST RBRACKET (ExprArray (ARRAY_CELL_LIST))
  | LITERAL_FLOAT (ExprLiteralFloat (LITERAL_FLOAT))
  | LITERAL_INT (ExprLiteralInt (LITERAL_INT))
  | LITERAL_STRING (ExprLiteralString (LITERAL_STRING))
  | LPAREN EXPR_NODE RPAREN (ExprParen (EXPR_NODE))
  | MINUS EXPR_NODE %prec UNARY_MINUS (ExprUnop (Minus, EXPR_NODE))
  | TRUE (ExprLiteralBool (True))

EXPR_NODE
  : EXPR (node (EXPR, EXPRleft, EXPRright))

FORMAL_PARAM
  : LAST_FORMAL_PARAM COMMA_OR_EOL (LAST_FORMAL_PARAM)

FORMAL_PARAM_NODE
  : FORMAL_PARAM (node (FORMAL_PARAM, FORMAL_PARAMleft, FORMAL_PARAMright))

FORMAL_PARAM_NODE_ANNOTATED
  : FORMAL_PARAM_NODE (([], FORMAL_PARAM_NODE, []))
  | PRE_ANNOTATION_LIST FORMAL_PARAM_NODE (
      (PRE_ANNOTATION_LIST, FORMAL_PARAM_NODE, [])
    ) 
  | FORMAL_PARAM_NODE POST_ANNOTATION_LIST (
      ([], FORMAL_PARAM_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_FORMAL_PARAM_NODE POST_ANNOTATION_LIST (
      ([], LAST_FORMAL_PARAM_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST FORMAL_PARAM_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, FORMAL_PARAM_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_FORMAL_PARAM_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, LAST_FORMAL_PARAM_NODE, POST_ANNOTATION_LIST)
    ) 

FORMAL_PARAM_NODE_ANNOTATED_LIST
  : LAST_FORMAL_PARAM_NODE_ANNOTATED ([ LAST_FORMAL_PARAM_NODE_ANNOTATED ])
  | FORMAL_PARAM_NODE_ANNOTATED ([ FORMAL_PARAM_NODE_ANNOTATED ])
  | FORMAL_PARAM_NODE_ANNOTATED FORMAL_PARAM_NODE_ANNOTATED_LIST (
      FORMAL_PARAM_NODE_ANNOTATED :: FORMAL_PARAM_NODE_ANNOTATED_LIST
    )

LAST_COMPONENT_MEMBER
  : LAST_COMPONENT_MEMBER_NODE (ComponentMember ([], LAST_COMPONENT_MEMBER_NODE, []))
  | PRE_ANNOTATION_LIST LAST_COMPONENT_MEMBER_NODE (
      ComponentMember (PRE_ANNOTATION_LIST, LAST_COMPONENT_MEMBER_NODE, [])
    ) 

LAST_COMPONENT_MEMBER_NODE
  : DEF_ARRAY_NODE (ComponentDefArray DEF_ARRAY_NODE)
  | DEF_CONSTANT_NODE (ComponentDefConstant DEF_CONSTANT_NODE)
  | DEF_ENUM_NODE (ComponentDefEnum DEF_ENUM_NODE)
  | DEF_STRUCT_NODE (ComponentDefStruct DEF_STRUCT_NODE)

LAST_DEF_ENUM_CONSTANT
  : IDENT EQUALS EXPR_NODE (DefEnumConstant (IDENT, SOME EXPR_NODE))
  | IDENT (DefEnumConstant (IDENT, NONE))

LAST_DEF_ENUM_CONSTANT_NODE
  : LAST_DEF_ENUM_CONSTANT (node (LAST_DEF_ENUM_CONSTANT, LAST_DEF_ENUM_CONSTANTleft, LAST_DEF_ENUM_CONSTANTright))

LAST_DEF_ENUM_CONSTANT_NODE_ANNOTATED
  : LAST_DEF_ENUM_CONSTANT_NODE (([], LAST_DEF_ENUM_CONSTANT_NODE, [])) 
  | PRE_ANNOTATION_LIST LAST_DEF_ENUM_CONSTANT_NODE (
      (PRE_ANNOTATION_LIST, LAST_DEF_ENUM_CONSTANT_NODE, [])
    ) 

LAST_FORMAL_PARAM
  : IDENT COLON TYPE_NAME_NODE (
      FormalParam (FormalParamValue, IDENT, TYPE_NAME_NODE, NONE)
    )
  | IDENT COLON TYPE_NAME_NODE SIZE EXPR_NODE(
      FormalParam (FormalParamValue, IDENT, TYPE_NAME_NODE, SOME EXPR_NODE)
    )
  | REF IDENT COLON TYPE_NAME_NODE (
      FormalParam (FormalParamRef, IDENT, TYPE_NAME_NODE, NONE)
    )
  | REF IDENT COLON TYPE_NAME_NODE SIZE EXPR_NODE(
      FormalParam (FormalParamRef, IDENT, TYPE_NAME_NODE, SOME EXPR_NODE)
    )

LAST_FORMAL_PARAM_NODE
  : LAST_FORMAL_PARAM (node (LAST_FORMAL_PARAM, LAST_FORMAL_PARAMleft, LAST_FORMAL_PARAMright))

LAST_FORMAL_PARAM_NODE_ANNOTATED
  : LAST_FORMAL_PARAM_NODE (([], LAST_FORMAL_PARAM_NODE, [])) 
  | PRE_ANNOTATION_LIST LAST_FORMAL_PARAM_NODE (
      (PRE_ANNOTATION_LIST, LAST_FORMAL_PARAM_NODE, [])
    ) 

LAST_STRUCT_MEMBER
  : IDENT EQUALS EXPR_NODE (StructMember (IDENT, EXPR_NODE))

LAST_STRUCT_TYPE_MEMBER
  : IDENT COLON TYPE_NAME_NODE (StructTypeMember (IDENT, TYPE_NAME_NODE))

LAST_STRUCT_TYPE_MEMBER_NODE
  : LAST_STRUCT_TYPE_MEMBER (node (LAST_STRUCT_TYPE_MEMBER, LAST_STRUCT_TYPE_MEMBERleft, LAST_STRUCT_TYPE_MEMBERright))

LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED
  : LAST_STRUCT_TYPE_MEMBER_NODE (([], LAST_STRUCT_TYPE_MEMBER_NODE, [])) 
  | PRE_ANNOTATION_LIST LAST_STRUCT_TYPE_MEMBER_NODE (
      (PRE_ANNOTATION_LIST, LAST_STRUCT_TYPE_MEMBER_NODE, [])
    ) 

LAST_TU_MEMBER
  : LAST_TU_MEMBER_NODE (TUMember ([], LAST_TU_MEMBER_NODE, []))
  | PRE_ANNOTATION_LIST LAST_TU_MEMBER_NODE (
      TUMember (PRE_ANNOTATION_LIST, LAST_TU_MEMBER_NODE, [])
    ) 

LAST_TU_MEMBER_NODE
  : DEF_ABS_TYPE_NODE (TUDefAbsType DEF_ABS_TYPE_NODE)
  | DEF_ARRAY_NODE (TUDefArray DEF_ARRAY_NODE)
  | DEF_COMPONENT_NODE (TUDefComponent DEF_COMPONENT_NODE)
  | DEF_CONSTANT_NODE (TUDefConstant DEF_CONSTANT_NODE)
  | DEF_ENUM_NODE (TUDefEnum DEF_ENUM_NODE)
  | DEF_MODULE_NODE (TUDefModule DEF_MODULE_NODE)
  | DEF_PORT_NODE (TUDefPort DEF_PORT_NODE)
  | DEF_STRUCT_NODE (TUDefStruct DEF_STRUCT_NODE)
  | SPEC_LOC_NODE (TUSpecLoc SPEC_LOC_NODE)

POST_ANNOTATION_LIST
  : POST_ANNOTATION ([ POST_ANNOTATION ])
  | POST_ANNOTATION POST_ANNOTATION_LIST (POST_ANNOTATION :: POST_ANNOTATION_LIST)

PRE_ANNOTATION_LIST
  : PRE_ANNOTATION ([ PRE_ANNOTATION ])
  | PRE_ANNOTATION PRE_ANNOTATION_LIST (PRE_ANNOTATION :: PRE_ANNOTATION_LIST)

QUAL_IDENT
  : IDENT ([ IDENT ])
  | IDENT DOT QUAL_IDENT (IDENT :: QUAL_IDENT)

SEMI_OR_EOL
  : SEMI (())
  | EOL (())

SPEC_LOC
  : LOCATE CONSTANT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocConstant, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE TYPE QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocType, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE PORT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocPort, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE COMPONENT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocComponent, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE COMPONENT INSTANCE QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocComponentInstance, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE TOPOLOGY QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocTopology, QUAL_IDENT, LITERAL_STRING)
    )

SPEC_LOC_NODE
  : SPEC_LOC (node (SPEC_LOC, SPEC_LOCleft, SPEC_LOCright))

STRUCT_MEMBER
  : LAST_STRUCT_MEMBER COMMA_OR_EOL (LAST_STRUCT_MEMBER)

STRUCT_MEMBER_LIST
  : LAST_STRUCT_MEMBER ([ LAST_STRUCT_MEMBER ])
  | STRUCT_MEMBER ([ STRUCT_MEMBER ])
  | STRUCT_MEMBER STRUCT_MEMBER_LIST (STRUCT_MEMBER :: STRUCT_MEMBER_LIST)

STRUCT_TYPE_MEMBER
  : LAST_STRUCT_TYPE_MEMBER COMMA_OR_EOL (LAST_STRUCT_TYPE_MEMBER)

STRUCT_TYPE_MEMBER_NODE
  : STRUCT_TYPE_MEMBER (node (STRUCT_TYPE_MEMBER, STRUCT_TYPE_MEMBERleft, STRUCT_TYPE_MEMBERright))

STRUCT_TYPE_MEMBER_NODE_ANNOTATED
  : STRUCT_TYPE_MEMBER_NODE (([], STRUCT_TYPE_MEMBER_NODE, []))
  | PRE_ANNOTATION_LIST STRUCT_TYPE_MEMBER_NODE (
      (PRE_ANNOTATION_LIST, STRUCT_TYPE_MEMBER_NODE, [])
    ) 
  | STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      ([], STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      ([], LAST_STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, LAST_STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST
  : LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED ([ LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED ])
  | STRUCT_TYPE_MEMBER_NODE_ANNOTATED ([ STRUCT_TYPE_MEMBER_NODE_ANNOTATED ])
  | STRUCT_TYPE_MEMBER_NODE_ANNOTATED STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST (
      STRUCT_TYPE_MEMBER_NODE_ANNOTATED :: STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST
    )

TRANS_UNIT
  : (TransUnit [])
  | TU_MEMBER_LIST (TransUnit TU_MEMBER_LIST)
  | EOL TRANS_UNIT (TRANS_UNIT)

TU_MEMBER
  : TU_MEMBER_NODE (TUMember ([], TU_MEMBER_NODE, [])) 
  | PRE_ANNOTATION_LIST TU_MEMBER_NODE (
      TUMember (PRE_ANNOTATION_LIST, TU_MEMBER_NODE, [])
    ) 
  | TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember ([], TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember ([], LAST_TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember (PRE_ANNOTATION_LIST, TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember (PRE_ANNOTATION_LIST, LAST_TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

TU_MEMBER_LIST
  : LAST_TU_MEMBER ([ LAST_TU_MEMBER ])
  | TU_MEMBER ([ TU_MEMBER ])
  | TU_MEMBER TU_MEMBER_LIST (TU_MEMBER :: TU_MEMBER_LIST)

TU_MEMBER_NODE
  : LAST_TU_MEMBER_NODE SEMI_OR_EOL (LAST_TU_MEMBER_NODE)

TYPE_NAME
  : BOOL (TypeNameBool)
  | STRING (TypeNameString)
  | TYPE_NAME_FLOAT (TYPE_NAME_FLOAT)
  | TYPE_NAME_INT (TYPE_NAME_INT)
  | QUAL_IDENT (TypeNameQualIdent QUAL_IDENT)

TYPE_NAME_FLOAT
  : F32 (TypeNameFloat F32)
  | F64 (TypeNameFloat F64)

TYPE_NAME_INT
  : I8 (TypeNameInt I8)
  | I16 (TypeNameInt I16)
  | I32 (TypeNameInt I32)
  | I64 (TypeNameInt I64)
  | U8 (TypeNameInt U8)
  | U16 (TypeNameInt U16)
  | U32 (TypeNameInt U32)
  | U64 (TypeNameInt U64)

TYPE_NAME_NODE
  : TYPE_NAME (node (TYPE_NAME, TYPE_NAMEleft, TYPE_NAMEright))

