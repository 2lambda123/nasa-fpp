(* ----------------------------------------------------------------------
 * fpp.grm
 * ----------------------------------------------------------------------*) 

open Ast
open AstNode
open Loc

fun pos1 node = 
  let val Loc {pos1,...} = Locations.lookup node
  in pos1 end

fun pos2 node = 
  let val Loc {pos2,...} = Locations.lookup node
  in pos2 end

fun node (e, pos1, pos2) =
let
  val node = AstNode.node e
in
  Locations.insert (node, Loc {
    file=(!ParserState.file),
    pos1=pos1,
    pos2=pos2
  });
  node
end

%%

%pos int

%eop EOF

%term 
  ARRAY
| AT
| BOOL
| COLON
| COMMA
| COMPONENT
| CONSTANT
| DEFAULT
| DOT
| ENUM
| EOF
| EOL
| EQUALS
| FALSE
| F32
| F64
| I16
| I32
| I64
| I8
| IDENT of string
| INSTANCE
| LBRACE
| LBRACKET
| LITERAL_BOOL of string
| LITERAL_FLOAT of string
| LITERAL_INT of string
| LITERAL_STRING of string
| LPAREN
| LOCATE
| MINUS
| MODULE
| PLUS
| PORT
| POST_ANNOTATION of string
| PRE_ANNOTATION of string
| RBRACE
| RBRACKET
| RPAREN
| SEMI
| STRING
| STRUCT
| TOPOLOGY
| TRUE
| TYPE
| U16
| U32
| U64
| U8

%nonterm 
  START of trans_unit
| ARRAY_CELL of expr node
| ARRAY_CELL_LIST of expr node list
| COMMA_OR_EOL of unit
| DEF_ABS_TYPE of def_abs_type
| DEF_ABS_TYPE_NODE of def_abs_type node
| DEF_ARRAY of def_array
| DEF_ARRAY_NODE of def_array node
| DEF_CONSTANT of def_constant
| DEF_CONSTANT_NODE of def_constant node
| DEF_ENUM of def_enum
| DEF_ENUM_NODE of def_enum node
| DEF_MODULE of def_module
| DEF_MODULE_NODE of def_module node
| DEF_STRUCT of def_struct
| DEF_STRUCT_NODE of def_struct node
| ENUMERATOR of enumerator
| ENUMERATOR_NODE of enumerator node
| ENUMERATOR_NODE_ANNOTATED of enumerator node annotated
| ENUMERATOR_NODE_ANNOTATED_LIST of enumerator node annotated list
| EOL_LBRACE_EOL of unit
| EXPR of expr
| EXPR_ATOMIC of expr
| EXPR_ATOMIC_NODE of expr node
| EXPR_NODE of expr node
| EXPR_POSTFIX of expr
| EXPR_POSTFIX_NODE of expr node
| EXPR_UNOP of expr
| EXPR_UNOP_NODE of expr node
| LAST_ENUMERATOR of enumerator
| LAST_ENUMERATOR_NODE of enumerator node
| LAST_ENUMERATOR_NODE_ANNOTATED of enumerator node annotated
| LAST_STRUCT_MEMBER of struct_member
| LAST_STRUCT_TYPE_MEMBER of struct_type_member
| LAST_STRUCT_TYPE_MEMBER_NODE of struct_type_member node
| LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED of struct_type_member node annotated
| LAST_TU_MEMBER of tu_member
| LAST_TU_MEMBER_NODE of tu_member_node
| POST_ANNOTATION_LIST of string list
| PRE_ANNOTATION_LIST of string list
| QUAL_IDENT of ident list
| SEMI_OR_EOL of unit
| SPEC_LOC of spec_loc
| SPEC_LOC_NODE of spec_loc node
| STRUCT_MEMBER of struct_member
| STRUCT_MEMBER_LIST of struct_member list
| STRUCT_TYPE_MEMBER of struct_type_member
| STRUCT_TYPE_MEMBER_NODE of struct_type_member node
| STRUCT_TYPE_MEMBER_NODE_ANNOTATED of struct_type_member node annotated
| STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST of struct_type_member node annotated list
| TRANS_UNIT of trans_unit
| TU_MEMBER of tu_member
| TU_MEMBER_LIST of tu_member list
| TU_MEMBER_NODE of tu_member_node
| TYPE_NAME of type_name
| TYPE_NAME_FLOAT of type_name
| TYPE_NAME_IDENT of type_name
| TYPE_NAME_INT of type_name
| TYPE_NAME_NODE of type_name node

%name FPP

%noshift EOF
%nodefault
%%

START 
  : TRANS_UNIT (TRANS_UNIT)

ARRAY_CELL
  : EXPR_NODE COMMA_OR_EOL (EXPR_NODE)

ARRAY_CELL_LIST
  : EXPR_NODE ([ EXPR_NODE ])
  | ARRAY_CELL ARRAY_CELL_LIST (ARRAY_CELL :: ARRAY_CELL_LIST)

COMMA_OR_EOL
  : COMMA (())
  | EOL (())

DEF_ABS_TYPE
  : TYPE IDENT (DefAbsType IDENT)

DEF_ABS_TYPE_NODE
  : DEF_ABS_TYPE (node (DEF_ABS_TYPE, DEF_ABS_TYPEleft, DEF_ABS_TYPEright))

DEF_ARRAY
  : ARRAY IDENT EQUALS LBRACKET EXPR_NODE RBRACKET TYPE_NAME_NODE (
      DefArray (IDENT, EXPR_NODE, TYPE_NAME_NODE, NONE)
    )
  | ARRAY IDENT EQUALS LBRACKET EXPR_NODE RBRACKET TYPE_NAME_NODE DEFAULT EXPR_NODE (
      DefArray (IDENT, EXPR_NODE, TYPE_NAME_NODE, SOME EXPR_NODE)
    )

DEF_ARRAY_NODE
  : DEF_ARRAY (node (DEF_ARRAY, DEF_ARRAYleft, DEF_ARRAYright))

DEF_CONSTANT
  : CONSTANT IDENT EQUALS EXPR_NODE (DefConstant (IDENT, EXPR_NODE))

DEF_CONSTANT_NODE
  : DEF_CONSTANT (node (DEF_CONSTANT, DEF_CONSTANTleft, DEF_CONSTANTright))

DEF_ENUM
  : ENUM IDENT LBRACE RBRACE (
      DefEnum (IDENT, NONE, [])
    )
  | ENUM IDENT LBRACE ENUMERATOR_NODE_ANNOTATED_LIST RBRACE (
      DefEnum (IDENT, NONE, ENUMERATOR_NODE_ANNOTATED_LIST)
    )
  | ENUM IDENT COLON TYPE_NAME_NODE LBRACE ENUMERATOR_NODE_ANNOTATED_LIST RBRACE (
      DefEnum (IDENT, SOME TYPE_NAME_NODE, ENUMERATOR_NODE_ANNOTATED_LIST)
    )
  | ENUM IDENT COLON TYPE_NAME_NODE LBRACE RBRACE (
      DefEnum (IDENT, SOME TYPE_NAME_NODE, [])
    )

DEF_ENUM_NODE
  : DEF_ENUM (node (DEF_ENUM, DEF_ENUMleft, DEF_ENUMright))

DEF_MODULE
  : MODULE IDENT LBRACE RBRACE (DefModule (IDENT, []))
  | MODULE IDENT LBRACE TU_MEMBER_LIST RBRACE (
      DefModule (IDENT, TU_MEMBER_LIST)
    )

DEF_MODULE_NODE
  : DEF_MODULE (node (DEF_MODULE, DEF_MODULEleft, DEF_MODULEright))

DEF_STRUCT
  : STRUCT IDENT LBRACE STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST RBRACE (
      DefStruct (IDENT, STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST, NONE)
    )
  | STRUCT IDENT LBRACE STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST RBRACE DEFAULT EXPR_NODE (
      DefStruct (IDENT, STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST, SOME EXPR_NODE)
    )

DEF_STRUCT_NODE
  : DEF_STRUCT (node (DEF_STRUCT, DEF_STRUCTleft, DEF_STRUCTright))

ENUMERATOR
  : LAST_ENUMERATOR COMMA_OR_EOL (LAST_ENUMERATOR)

ENUMERATOR_NODE
  : ENUMERATOR (node (ENUMERATOR, ENUMERATORleft, ENUMERATORright))

ENUMERATOR_NODE_ANNOTATED
  : ENUMERATOR_NODE (([], ENUMERATOR_NODE, [])) 
  | PRE_ANNOTATION_LIST ENUMERATOR_NODE (
      (PRE_ANNOTATION_LIST, ENUMERATOR_NODE, [])
    ) 
  | ENUMERATOR_NODE POST_ANNOTATION_LIST (
      ([], ENUMERATOR_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_ENUMERATOR_NODE POST_ANNOTATION_LIST (
      ([], LAST_ENUMERATOR_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST ENUMERATOR_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, ENUMERATOR_NODE, POST_ANNOTATION_LIST)
    ) 

ENUMERATOR_NODE_ANNOTATED_LIST
  : LAST_ENUMERATOR_NODE_ANNOTATED ([ LAST_ENUMERATOR_NODE_ANNOTATED ])
  | ENUMERATOR_NODE_ANNOTATED ([ ENUMERATOR_NODE_ANNOTATED ])
  | ENUMERATOR_NODE_ANNOTATED ENUMERATOR_NODE_ANNOTATED_LIST (
      ENUMERATOR_NODE_ANNOTATED :: ENUMERATOR_NODE_ANNOTATED_LIST
    )

EXPR
  : EXPR_UNOP (EXPR_UNOP)
  | LBRACKET ARRAY_CELL_LIST RBRACKET (ExprArray (ARRAY_CELL_LIST))
  | LBRACE STRUCT_MEMBER_LIST RBRACE (ExprStruct (STRUCT_MEMBER_LIST))

EXPR_ATOMIC
  : IDENT (ExprIdent IDENT)
  | TRUE (ExprLiteralBool (True))
  | FALSE (ExprLiteralBool (False))
  | LITERAL_FLOAT (ExprLiteralFloat (LITERAL_FLOAT))
  | LITERAL_INT (ExprLiteralInt (LITERAL_INT))
  | LITERAL_STRING (ExprLiteralString (LITERAL_STRING))

EXPR_NODE
  : EXPR (node (EXPR, EXPRleft, EXPRright))

EXPR_POSTFIX
  : EXPR_ATOMIC (EXPR_ATOMIC)
  | EXPR_POSTFIX_NODE DOT IDENT(ExprDot (EXPR_POSTFIX_NODE, IDENT))

EXPR_POSTFIX_NODE
  : EXPR_POSTFIX (node (EXPR_POSTFIX, EXPR_POSTFIXleft, EXPR_POSTFIXright))

EXPR_UNOP
  : EXPR_POSTFIX (EXPR_POSTFIX)
  | MINUS EXPR_UNOP_NODE (ExprUnop (Minus, EXPR_UNOP_NODE))

EXPR_UNOP_NODE
  : EXPR_UNOP (node (EXPR_UNOP, EXPR_UNOPleft, EXPR_UNOPright))

LAST_ENUMERATOR
  : IDENT EQUALS EXPR_NODE (Enumerator (IDENT, SOME EXPR_NODE))
  | IDENT (Enumerator (IDENT, NONE))

LAST_ENUMERATOR_NODE
  : LAST_ENUMERATOR (node (LAST_ENUMERATOR, LAST_ENUMERATORleft, LAST_ENUMERATORright))

LAST_ENUMERATOR_NODE_ANNOTATED
  : LAST_ENUMERATOR_NODE (([], LAST_ENUMERATOR_NODE, [])) 
  | PRE_ANNOTATION_LIST LAST_ENUMERATOR_NODE (
      (PRE_ANNOTATION_LIST, LAST_ENUMERATOR_NODE, [])
    ) 
  | PRE_ANNOTATION_LIST LAST_ENUMERATOR_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, LAST_ENUMERATOR_NODE, POST_ANNOTATION_LIST)
    ) 

LAST_STRUCT_MEMBER
  : IDENT EQUALS EXPR_NODE (StructMember (IDENT, EXPR_NODE))

LAST_STRUCT_TYPE_MEMBER
  : IDENT COLON TYPE_NAME_NODE (StructTypeMember (IDENT, TYPE_NAME_NODE))

LAST_STRUCT_TYPE_MEMBER_NODE
  : LAST_STRUCT_TYPE_MEMBER (node (LAST_STRUCT_TYPE_MEMBER, LAST_STRUCT_TYPE_MEMBERleft, LAST_STRUCT_TYPE_MEMBERright))

LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED
  : LAST_STRUCT_TYPE_MEMBER_NODE (([], LAST_STRUCT_TYPE_MEMBER_NODE, [])) 
  | PRE_ANNOTATION_LIST LAST_STRUCT_TYPE_MEMBER_NODE (
      (PRE_ANNOTATION_LIST, LAST_STRUCT_TYPE_MEMBER_NODE, [])
    ) 
  | PRE_ANNOTATION_LIST LAST_STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, LAST_STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

LAST_TU_MEMBER
  : LAST_TU_MEMBER_NODE (TUMember ([], LAST_TU_MEMBER_NODE, []))
  | PRE_ANNOTATION_LIST LAST_TU_MEMBER_NODE (
      TUMember (PRE_ANNOTATION_LIST, LAST_TU_MEMBER_NODE, [])
    ) 

LAST_TU_MEMBER_NODE
  : DEF_ABS_TYPE_NODE (TUDefAbsType DEF_ABS_TYPE_NODE)
  | DEF_ARRAY_NODE (TUDefArray DEF_ARRAY_NODE)
  | DEF_CONSTANT_NODE (TUDefConstant DEF_CONSTANT_NODE)
  | DEF_ENUM_NODE (TUDefEnum DEF_ENUM_NODE)
  | DEF_MODULE_NODE (TUDefModule DEF_MODULE_NODE)
  | DEF_STRUCT_NODE (TUDefStruct DEF_STRUCT_NODE)
  | SPEC_LOC_NODE (TUSpecLoc SPEC_LOC_NODE)

POST_ANNOTATION_LIST
  : POST_ANNOTATION ([ POST_ANNOTATION ])
  | POST_ANNOTATION POST_ANNOTATION_LIST (POST_ANNOTATION :: POST_ANNOTATION_LIST)

PRE_ANNOTATION_LIST
  : PRE_ANNOTATION ([ PRE_ANNOTATION ])
  | PRE_ANNOTATION PRE_ANNOTATION_LIST (PRE_ANNOTATION :: PRE_ANNOTATION_LIST)

QUAL_IDENT
  : IDENT ([ IDENT ])
  | IDENT DOT QUAL_IDENT (IDENT :: QUAL_IDENT)

SEMI_OR_EOL
  : SEMI (())
  | EOL (())

SPEC_LOC
  : LOCATE CONSTANT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocConstant, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE TYPE QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocType, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE PORT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocPort, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE COMPONENT QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocComponent, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE COMPONENT INSTANCE QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocComponentInstance, QUAL_IDENT, LITERAL_STRING)
    )
  | LOCATE TOPOLOGY QUAL_IDENT AT LITERAL_STRING (
      SpecLoc (SpecLocTopology, QUAL_IDENT, LITERAL_STRING)
    )

SPEC_LOC_NODE
  : SPEC_LOC (node (SPEC_LOC, SPEC_LOCleft, SPEC_LOCright))

STRUCT_MEMBER
  : LAST_STRUCT_MEMBER COMMA_OR_EOL (LAST_STRUCT_MEMBER)

STRUCT_MEMBER_LIST
  : LAST_STRUCT_MEMBER ([ LAST_STRUCT_MEMBER ])
  | STRUCT_MEMBER ([ STRUCT_MEMBER ])
  | STRUCT_MEMBER STRUCT_MEMBER_LIST (STRUCT_MEMBER :: STRUCT_MEMBER_LIST)

STRUCT_TYPE_MEMBER
  : LAST_STRUCT_TYPE_MEMBER COMMA_OR_EOL (LAST_STRUCT_TYPE_MEMBER)

STRUCT_TYPE_MEMBER_NODE
  : STRUCT_TYPE_MEMBER (node (STRUCT_TYPE_MEMBER, STRUCT_TYPE_MEMBERleft, STRUCT_TYPE_MEMBERright))

STRUCT_TYPE_MEMBER_NODE_ANNOTATED
  : STRUCT_TYPE_MEMBER_NODE (([], STRUCT_TYPE_MEMBER_NODE, []))
  | PRE_ANNOTATION_LIST STRUCT_TYPE_MEMBER_NODE (
      (PRE_ANNOTATION_LIST, STRUCT_TYPE_MEMBER_NODE, [])
    ) 
  | STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      ([], STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      ([], LAST_STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST STRUCT_TYPE_MEMBER_NODE POST_ANNOTATION_LIST (
      (PRE_ANNOTATION_LIST, STRUCT_TYPE_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST
  : LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED ([ LAST_STRUCT_TYPE_MEMBER_NODE_ANNOTATED ])
  | STRUCT_TYPE_MEMBER_NODE_ANNOTATED ([ STRUCT_TYPE_MEMBER_NODE_ANNOTATED ])
  | STRUCT_TYPE_MEMBER_NODE_ANNOTATED STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST (
      STRUCT_TYPE_MEMBER_NODE_ANNOTATED :: STRUCT_TYPE_MEMBER_NODE_ANNOTATED_LIST
    )

TRANS_UNIT
  : (TransUnit [])
  | TU_MEMBER_LIST (TransUnit TU_MEMBER_LIST)
  | EOL TRANS_UNIT (TRANS_UNIT)

TU_MEMBER
  : TU_MEMBER_NODE (TUMember ([], TU_MEMBER_NODE, [])) 
  | PRE_ANNOTATION_LIST TU_MEMBER_NODE (
      TUMember (PRE_ANNOTATION_LIST, TU_MEMBER_NODE, [])
    ) 
  | TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember ([], TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | LAST_TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember ([], LAST_TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember (PRE_ANNOTATION_LIST, TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 
  | PRE_ANNOTATION_LIST LAST_TU_MEMBER_NODE POST_ANNOTATION_LIST (
      TUMember (PRE_ANNOTATION_LIST, LAST_TU_MEMBER_NODE, POST_ANNOTATION_LIST)
    ) 

TU_MEMBER_LIST
  : LAST_TU_MEMBER ([ LAST_TU_MEMBER ])
  | TU_MEMBER ([ TU_MEMBER ])
  | TU_MEMBER TU_MEMBER_LIST (TU_MEMBER :: TU_MEMBER_LIST)

TU_MEMBER_NODE
  : LAST_TU_MEMBER_NODE SEMI_OR_EOL (LAST_TU_MEMBER_NODE)

TYPE_NAME
  : BOOL (TypeNameBool)
  | STRING (TypeNameString)
  | TYPE_NAME_FLOAT (TYPE_NAME_FLOAT)
  | TYPE_NAME_INT (TYPE_NAME_INT)
  | QUAL_IDENT (TypeNameIdentList QUAL_IDENT)

TYPE_NAME_FLOAT
  : F32 (TypeNameFloat F32)
  | F64 (TypeNameFloat F64)

TYPE_NAME_INT
  : I8 (TypeNameInt I8)
  | I16 (TypeNameInt I16)
  | I32 (TypeNameInt I32)
  | I64 (TypeNameInt I64)
  | U8 (TypeNameInt U8)
  | U16 (TypeNameInt U16)
  | U32 (TypeNameInt U32)
  | U64 (TypeNameInt U64)

TYPE_NAME_NODE
  : TYPE_NAME (node (TYPE_NAME, TYPE_NAMEleft, TYPE_NAMEright))

