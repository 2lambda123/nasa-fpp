== Defining Ports

A *port definition* defines an F Prime port.
In F Prime, a port specifies the endpoint of a connection
between two component instances.
Components are the basic units of FSW function in F Prime
and are described in the
<<Defining-Components,next section>>.
A port definition specifies (1) the name of the port, (2) the type of the data 
carried
on the port, and (3) an optional return type.

=== Port Names

The simplest port definition consists of the keyword `port` followed
by a name.
For example:

[source,fpp]
----
port P
----

This code defines a port named `P` that carries no data and returns
no data.
This kind of port can be useful for sending or receiving a triggering event.

=== Formal Parameters

More often, a port will carry data.
To specify the data, you write *formal parameters*
enclosed in parentheses.
The formal parameters of a port definition are similar to the formal parameters 
of a function in a programming
language: each one has a name and a type, and you may write
zero or more of them.
For example:

[source,fpp]
----
port P1() # Zero parameters; equivalent to port P1
port P2(a: U32) # One parameter
port P3(a: I32, b: F32, c: string) # Three parameters
----

The type of a formal parameter may be any valid type, including an
array type, a struct type, an enum type, or an abstract type.
For example, here is some code that defines an enum type `E` and
and abstract type `T`, and then uses those types in the
formal parameters of a port:

[source,fpp]
----
enum E { A, B }
type T
port P(e: E, t: T)
----

Each formal parameter of a port _P_ becomes bound to an argument (a value) when 
_P_ is *invoked*.
The invocation occurs in {cpp} code that implements the model.
To invoke a port, you call an auto-generated function.
The arguments are {cpp} values, and their types must match the
types of the {cpp} function parameters generated from the FPP port parameters.
The invocation may be *synchronous* (via a direct function call)
or *asynchronous* (via a message queue);
we will discuss these cases further below in the section on
<<Defining-Components,defining components>>.

The formal parameters form an
<<Defining-Constants_Multiple-Definitions-and-Element-Sequences,element
sequence>> in which the optional terminating punctuation is a comma.  As usual
for element sequences, you can omit the comma and use a newline instead.  So,
for example, we can write the definition shown above in this alternate way:

[source,fpp]
----
enum E { A, B }
type T
port P(
  e: E
  t: T
)
----

=== Ref Parameters

You may write the keyword `ref` in front of any formal parameter _p_
of a port definition.
Doing this specifies that _p_ is a *reference parameter*, i.e.,
its data is passed by reference when the passing happens in a
synchronous invocation.
For example:

[source,fpp]
----
type T
port P(ref a: T)
----

In this example, `a` is a reference parameter of type `T`.
In a synchronous invocation, `a` is passed by reference.
In an asynchronous invocation, `a` is passed by value
(it is serialized onto a queue).
The `ref` marking does not cause pass by reference in this case.

When a reference parameter is used in a synchronous port invocation,
we say that the parameter has 
*pass-by-reference semantics*.
In other cases, the parameter is passed by value, and we say that it
has *pass-by-value semantics*.
In particular, a parameter is always passed by value if it is not
marked `ref`.
In this case, we say that it is a *value parameter*.

There are at least two reasons to use pass-by-reference semantics:

. To avoid copying a large data value.
For example, in the code shown above, if `T` is an image with a large physical
size, then the `ref` marking on parameter `a` saves a copy of the image when 
performing a synchronous invocation on the port `P`.

. To return a value to the sender by storing it through
the reference.
We discuss this case in the next section on returning
values.

When using pass-by-reference semantics,
you must carefully manage the
the use of the data to avoid concurrency bugs
such as data races.
Except in special cases that require special expertise (e.g., 
the implementation of highly concurrent data structures),
you should enforce the rule that at most
one component may use any piece of data at any time.
In particular, if component `A` passes a reference to component `B`,
then component `A` should not use the reference while
component `B` is using it, and vice versa.
For example:

. Suppose component `A` owns some data `D` and passes a reference
to `D` via a synchronous port call to component `B`.
Suppose the port handler in component `B` uses the data but
does not store the reference, so that when the handler exits,
the reference is lost.
This is a good pattern.
In this case, we may say that ownership of `D` resides in `A`, temporarily
goes to `B` for the life of the handler, and goes back to `A`
when the handler exits.
Because the port call is synchronous, the handler in `B`
never runs concurrently with any code in `A` that uses `D`.
So at most one of `A` or `B` uses `D` at any time.

. Suppose instead that the handler in `B` stores the reference
into a member variable, so that the reference
persists after the handler exits.
If this happens, then you should make sure that `A` cannot use
`D` unless and until `B` passes ownership of `D` to `A` and vice versa.
For example, you could use state variables of enum type in `A` and in `B` to 
track ownership, and you could have a port invocation from `A` to `B` pass the 
reference and transfer ownership from `A` to `B` and vice versa.

When you use pass-by-value semantics, you don't have to worry
about these issues, because the framework will copy the data
for you. You can achieve pass-by-value semantics in three ways:

. Using a value parameter in a synchronous or asynchronous invocation.

. Using a reference parameter in an asynchronous invocation.

. Using a reference parameter in a synchronous invocation,
immediately coping the data in the handler
that receives the parameter, and thereafter using the copy,
never using the reference again.

*XML limitation:* In the current version of the F Prime XML-to-{cpp}
autocoder, if a parameter `a` of a port `P` carries a large value, 
then you should mark `a` `ref` if you intend to use `P` in an asynchronous
invocation.
Doing this avoids unnecessary copies that will otherwise occur in the.
auto-generated code.
If you also need to use `P` in a synchronous invocation, and you need
pass-by-value semantics in that invocation, then you can copy the
data in the handler, as discussed above.

This use of `ref` is unfortunate: it is an autocoder optimization
that has leaked into the model description.
In a future version of the autocoder, we intend to perform
the optimization automatically, making this use of `ref` unnecessary.

In summary, given the current state of the XML autocoder, you should
use a `ref` parameter if (1) you want pass-by-reference semantics
in a synchronous invocation (the intended use of `ref`); or (2)
the parameter carries large data and you intend to use it in an asynchronous
invocation (the result of an autocoder quirk, which will be fixed in
a future version of F Prime).

=== Returning Values

Optionally, you can give the port a return type.
To do this you write an arrow `pass:[->]` and a type
after the name and the formal parameters, if any.
For example:

[source,fpp]
----
type T
port P1 -> U32 # No parameters, returns U32
port P2(a: U32, b: F32) -> T # Two parameters, returns T
----

Invoking a port with a return type is like calling a function with
a return value.
Such a port may be used only in a synchronous context (i.e.,
as a direct function call, not as a message placed
on a concurrent queue).

In a synchronous context only, `ref` parameters provide another way to return 
values on the port,
by assigning to the reference, instead of executing a {cpp} `return` statement.
As an example, consider the following two port definitions:

[source,fpp]
----
type T
port P1 -> T
port P2(ref t: T)
----

The similarities and differences are as follows:

. Both `P1` and `P2` must be used in a synchronous context,
because each returns a `T` value.

. In the generated {cpp} code,

.. The function for invoking `P1`
has no arguments and returns a `T` value.
A handler associated with `P1` returns a value of type `T`
via the {cpp} `return` statement.
For example:
+
----
T P1_handler() {
  ...
  return T(1, 2, 3);
}
----

.. The function for invoking `P1` has one argument `t`
of type `T&`.
A handler associated with `P2` returns a value of type `T`
by updating the reference `t` (assigning to it, or updating
its fields).
For example:
+
----
void P2_handler(T& t) {
  ...
  t = T(1, 2, 3);
}
----
+
+

The second way may involve less copying of data.

Finally, there can be any number of reference parameters,
but at most one return value.
So if you need to return multiple values on a port, then reference
parameters can be useful.
As an example, the following port attempts to update a result
value of type `U32`.
It does this via reference parameter.
It also returns a status value indicating whether the update
was successful.

[source,fpp]
----
enum Status { SUCCEED, FAIL }
port P(ref result: U32) -> Status
----

A handler for `P` might look like this:

----
Status P_handler(U32& result) {
  Status status = Status::FAIL;
  if (...) {
    ...
    result = ...
    status = Status::SUCCEED; 
  }
  return status;
}
----

=== Annotating a Port Definition

A port definition is an
<<Writing-Comments-and-Annotations_Annotations,annotatable element>>.
Each formal parameter is also an annotatable element.
Here is an example:

[source,fpp]
----
@ Pre annotation for port P
port P(
  @ Pre annotation for parameter a
  a: U32
  @ Pre annotation for parameter b
  b: F32
)
----
