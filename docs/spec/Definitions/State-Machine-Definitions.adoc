=== State Machine Definitions

A *state machine definition* defines a state machine.

There are two kinds of state machine definitions:

. An *external state machine definition* 
introduces a name and informs the FPP analyzer that
a state machine implementation with that name will be provided to the
FSW build.

. An *internal state machine definition*
specifies the behavior of a state machine and
causes an implementation to be generated.

*Implementation note:*
Support for state machines will be implemented in two phases.
Phase 1 will implement external state machine definitions only.
Phase 2 will add support for internal state machine definitions.

==== Syntax

`state machine` <<Lexical-Elements_Identifiers,_identifier_>> 
_[_ `{` _state-machine-member-sequence_ `}` _]_

_state-machine-member-sequence_ is an 
<<Element-Sequences,element sequence>> in
which each element is a *state machine member*,
and the terminating punctuation is a semicolon.
A state machine member is one of the following:

* An <<State-Machine-Behavior-Elements_Action-Definitions,action definition>>
* A <<State-Machine-Behavior-Elements_Guard-Definitions,guard definition>>
* A <<State-Machine-Behavior-Elements_Initial-Transition-Specifiers,initial transition specifier>>
* A <<State-Machine-Behavior-Elements_Junction-Definitions,junction definition>>
* A <<State-Machine-Behavior-Elements_State-Definitions,state definition>>
* A <<State-Machine-Behavior-Elements_State-Machine-Signal-Definitions,state machine signal definition>>

==== Semantics

. The identifier is the name of the state machine.

. If present, the optional state machine member sequence defines the
behavior of the state machine.
In this case, the state machine definition is internal.
If the state machine member sequence is absent, then the state machine
definition is external, i.e., the state machine
behavior is not specified in FPP.

. If present, the optional state machine member sequence must contain
exactly one
<<State-Machine-Behavior-Elements_Initial-Transition-Specifiers,initial transition specifier>>.
The initial transition occurs when the state machine starts up.

. In the optional state machine member sequence,
the set of <<State-Machine-Behavior-Elements_Enter-Expressions,
enter expressions>>
induces a directed graph called the *transition graph*.
In this graph, each enter expression _e_
represents an arc from an *initial node* to a *terminal node*.
The initial node is defined as follows:

.. If _e_ appears in an
<<State-Machine-Behavior-Elements_Initial-Transition-Specifiers,
initial transition specifier>> _I_, then the initial node
is _I_.

.. If _e_ appears in a
<<State-Machine-Behavior-Elements_State-Transition-Specifiers,
state transition specifier>> _T_, then the initial
node is the
<<State-Machine-Behavior-Elements_State-Definitions,state definition>>
immediately enclosing _T_.

.. If _e_ appears in a
<<State-Machine-Behavior-Elements_Junction-Definitions,junction definition>>
_J_, then the initial node is _J_.

+
In any case, 
the terminal node is the state definition or junction definition
<<Definitions_State-Machine-Definitions_Scoping-of-Names,referred to>>
in _e_ after the keyword `enter`.
A *path* in the transition graph is a finite sequence of arcs
such that, for each arc _A_ in the sequence, either (a) _A_
is the last arc in the sequence, or (b)
the terminal node of _A_ is the initial node of the arc that
follows _A_ in the sequence.
The transition graph must satisfy the following rules:

.. Each junction must be the terminal node of exactly one arc.

.. For any path _P_, either (a) _P_ contains no cycle or (b)
some initial or terminal node of some arc in _P_ is a state definition.

==== Scoping of Names

Inside the optional state machine member sequence, the following
rules govern the assignment of names to definitions and the resolution
of names in uses.

*State machine member definitions:*
There are five kinds of *state machine member definitions*:
Action definitions, guard definitions, junction definitions, state
definitions, and state machine signal definitions.

*Qualified names:*
Each state machine member definition has an associated qualified
name.
Within a state machine member sequence,
the association of names to state machine member definitions is
the same as for <<Scoping-of-Names_Names-of-Definitions,FPP definitions>>,
after replacing "`definition`" with "`state machine member definition.`"

For example:

[source,fpp]
----
state machine S {

  # Qualified name is A
  action A

  # Qualified name is S1
  state S1 {

    # Qualified name is S1.S2
    state S2

  }

}
----

*Conflicting names:*
No two state machine definitions of the same kind may have the
same qualified name.

*Resolution of names:*
Names are resolved in the ordinary way for
<<Scoping-of-Names_Resolution-of-Identifiers,identifiers>>
and
<<Scoping-of-Names_Resolution-of-Qualified-Identifiers,qualified identifiers>> in FPP,
with the following modifications:

. The top level is the state machine member sequence.

. The definitions are the state machine member definitions.

. Each kind of definition resides in its own name group.

. The brace-delimited definitions are state definitions.

==== Examples

[source,fpp]
----

state machine MonitorSm {

  signal Complete
  signal Drive
  signal Calibrate
  signal RTI
  signal Stop
  signal Fault
  
  action init2
  action doCalibrate
  action motorControl
  action reportFault

  guard calibrateReady

  initial enter DeviceOn
  
  state DeviceOn {

    initial do init2 enter Initializing

    state Initializing {
      on Complete enter Idle
    }

    state Idle {
      on Drive enter Driving
      on Calibrate if calibrateReady enter Calibrating
    }

    state Calibrating {
      on RTI do doCalibrate
      on Fault do reportFault enter Idle
      on Complete enter Idle
    }

    state Driving {
      on RTI do motorControl
      on Stop enter Idle
    }

  }

}
----
