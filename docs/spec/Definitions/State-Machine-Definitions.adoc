=== State Machine Definitions

A *state machine definition* defines a state machine.

==== Syntax

`state machine` <<Lexical-Elements_Identifiers,_identifier_>> 
_[_ `{` _state-machine-member-sequence_ `}` _]_

_state-machine-member-sequence_ is an 
<<Element-Sequences,element sequence>> in
which each element is a *state machine member*,
and the terminating punctuation is a semicolon.
A state machine member is one of the following:

* An <<State-Machine-Behavior_Action-Definitions,action definition>>
* A <<State-Machine-Behavior_State-Definition,state definition>>
* A <<State-Machine-Behavior_State-Event-Definitions,state event definition>>
* A <<State-Machine-Behavior_State-Guards,state guards>>
* A <<State-Machine-Behavior_State-Initial,state initial>>
* A <<State-Machine-Behavior_State-Junction,state junction>>

==== Semantics

. The identifier is the name of the state machine.

. If present, the optional state machine member sequence defines the
behavior of the state machine.
If the state machine member sequence is absent, then the behavior
is externally defined.

. The state machine must have one <<State-Machine-Behavior_State-Initial,state initial>>
that specifies the transition to the initial state.

==== Implementation Note

Support for state machines will be implemented in two phases.
Phase 1 will implement externally defined state machines only.
Phase 2 will add support for state machines defined in FPP.

==== Examples

[source,fpp]
----

state machine MonitorSm {

  event Complete
  event Drive
  event Calibrate
  event RTI
  event Stop
  event Fault
  
  action init2
  action doCalibrate
  action motorControl
  action reportFault

  guard calibrateReady

  initial DeviceOn
  
  state DeviceOn {

    initial Initializing do init2

    state Initializing {
      on Complete visit Idle
    }

    state Idle {
      on Drive visit Driving
      on Calibrate if calibrateReady visit Calibrating
    }

    state Calibrating {
      on RTI do doCalibrate
      on Fault go Idle do reportFault
      on Complete visit Idle
    }

    state Driving {
      on RTI do motorControl
      on Stop visit Idle
    }

  }

}
----
