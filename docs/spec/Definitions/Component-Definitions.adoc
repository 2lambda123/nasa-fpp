=== Component Definitions

A *component definition* defines an F Prime component.
A component is a unit of function in the F Prime framework.
Component instances communicate with each other across
<<Definitions_Port-Definitions,ports>>.

==== Syntax

_component-kind_ `component` 
<<Lexical-Elements_Identifiers,_identifier_>>
`{` _component-member-sequence_ `}`

_component-kind_ is one of the following:

* `active`

* `passive`

* `queued`

_component-member-sequence_ is an
<<Element-Sequences,element sequence>> in 
which each element is a *component member*,
and the terminating punctuation is a semicolon.
A component member is one of the following:

* A <<Definitions_Constant-Definitions,constant definition>>

* A <<Definitions_Struct-Definitions,struct definition>>

* A <<Specifiers_Command-Specifiers,command specifier>>.
All commands specified in a component must have distinct names and opcodes.

* A <<Specifiers_Parameter-Specifiers,parameter specifier>>.
All parameters specified in a component must have distinct names and identifier 
values.

* A <<Specifiers_Port-Instance-Specifiers,port instance specifier>>

* A <<Specifiers_Telemetry-Channel-Specifiers,telemetry channel specifier>>.
All telemetry channels specified in a component must have distinct names and 
identifier values.

* An <<Definitions_Array-Definitions,array definition>>

* An <<Definitions_Enum-Definitions,enum definition>>

* An <<Specifiers_Event-Specifiers,event specifier>>.
All events specified in a component must have distinct names and identifier 
values.

* An <<Specifiers_Include-Specifiers,include specifier>>

* An <<Specifiers_Internal-Port-Specifiers,internal port specifier>>
All internal ports specified in a component must have distinct names.

==== Semantics

The identifier is the name of the component.
The definitions inside the body of the component are
qualified with the component name, as for 
<<Definitions_Module-Definitions,module definitions>>.

The port definitions must satisfy the following rules:

. No passive component may have an `async` `input` port
instance definition.

. Each active component must have either a command port
or at least one `async` `input` port.

==== Examples

[source,fpp]
----
@ Receives commands from the ground or from a sequencer
@ Dispatches commands to other components
active component CommandDispatcher {

  # ----------------------------------------------------------------------
  # Constants 
  # ----------------------------------------------------------------------

  @ The number of com command input ports
  constant numComCmdInPorts = 10

  @ The number of command output ports
  constant numCmdOutPorts = 20

  # ----------------------------------------------------------------------
  # General ports 
  # ----------------------------------------------------------------------

  @ Dispatches commands
  output port cmdOut: [numCmdOutPorts] Fw.Cmd

  @ Forwards received command responses
  output port cmdResponseInOut: [numComCmdInPorts] Fw.CmdResponse

  @ Receives com packets containing serialized commands
  async input port comCmdIn: [numComCmdInPorts] Fw.Com
  
  @ Receives command registration requests
  guarded input port cmdRegIn: [numCmdOutPorts] Fw.CmdReg

  @ Receives command responses
  async input port cmdResponseIn: Fw.CmdResponse

  # ----------------------------------------------------------------------
  # Special ports 
  # ----------------------------------------------------------------------

  @ Command receive port
  command recv port cmdIn

  @ Command registration port
  command reg port cmdRegOut

  @ Command response port
  command resp cmdResponseOut

  @ Event port
  event port eventOut

  @ Telemetry port
  telemetry port tlmOut

  @ Text event port
  text event port textEventOut

  @ Time get port
  time get port timeGetOut

  # ----------------------------------------------------------------------
  # Commands 
  # ----------------------------------------------------------------------

  @ No-op command
  async command NO_OP

  @ No-op string command
  async command NO_OP_STRING(
      arg1: string size 256 @< The string command argument
  )

  ...

  # ----------------------------------------------------------------------
  # Events 
  # ----------------------------------------------------------------------

  @ Opcode registered event
  diagnostic event OpCodeRegistered(
      regOpcode: Opcode @< The opcode to register
      regPort: U32 @< The registration port
      dispatchSlot: U32 @< The dispatch slot
  ) format "Opcode {} registered to port {} slot {}"

  @ Opcode dispatched event
  command event OpCodeDispatched(
      dispatchOpcode: Opcode @< The opcode dispatched
      dispatchPort: U32 @< The dispatch port
  ) format "Opcode {} dispatched to port {}"

  ...

  # ---------------------------------------------------------------------- 
  # Telemetry
  # ---------------------------------------------------------------------- 

  @ Number of commands dispatched
  telemetry CommandsDispatched: U32 update on change

  @ Number of command errors
  telemetry CommandErrors: U32 update on change

}
----
