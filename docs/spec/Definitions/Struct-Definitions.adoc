=== Struct Definitions

A *struct definition* defines a new structure type and associates a name
with it.

==== Syntax

`struct` <<Lexical-Elements_Identifiers,_identifier_>>
`{` _struct-type-member-sequence_ `}`
_[_ `default` <<Expressions,_expression_>> _]_

_struct-type-member-sequence_ is an <<Element-Sequences,element sequence>>
in which the elements are struct type members, and the terminating
punctuation is a comma.
A *struct type member* has the following syntax:

<<Lexical-Elements_Identifiers,_identifier_>> `:` <<Type-Names,_type-name_>>
_[_
`format` <<Expressions_String-Literals,_string-literal_>>
_]_

==== Semantics

The identifier is the name _N_ of the type.  The definition associates the name
_N_ with a type, _T'_ representing a structure with named members, each
of the specified type.  Each
identifier appearing in the struct type member sequence must be distinct.

The optional format specifier for a struct member specifies a 
<<Format-Strings,format string>>.
The format is applied when displaying the struct member.
There is one argument to the format string, which is the member value.
A numeric format is allowed if the member type 
<<Types_Types-with-Numeric-Members,has numeric members>>. The format specified
here overrides any format specified in the member type.

The expression following the keyword _default_ is optional.
If present, it specifies the <<Types_Default-Values,default value>>
associated with the type.
The type of the expression must be
<<Type-Checking_Type-Conversion,convertible to>> _T'_.

The type name _N_ is not in scope in the body of the definition.

==== Examples

[source,fpp]
----
# Defines a struct type A with members x and y
# and default value { x = 0, y = 0 }
struct A {
  x: U32
  y: F32
}

# Defines a struct type B with members x and y
# and default value { x = 0, y = 1 }
struct B {
  x: U32
  y: F32
} default { y = 1 }

# Defines a struct type C with format specifiers
struct C {
  x: U32 format "{} counts"
  y: F32 format "{} m/s"
}
----
