=== State Definitions

A *state definition* defines a state of a 
<<Definitions_State-Machine-Definitions,state machine>>.

==== Syntax

`state` <<Lexical-Elements_Identifiers,_identifier_>>
_[_ `{` _state-definition-member-sequence_ `}` _]_

_state-definition-member-sequence_ is an 
<<Element-Sequences,element sequence>> in
which each element is a *state definition member*,
and the terminating punctuation is a semicolon.
A state definition member is one of the following

* A <<State-Machine-Behavior-Elements_Initial-Transition-Specifiers,initial transition specifier>>
* A <<State-Machine-Behavior-Elements_Junction-Definitions,junction definition>>
* A <<State-Machine-Behavior-Elements_State-Definitions,state definition>>
* A <<State-Machine-Behavior-Elements_State-Transition-Specifiers,state transition specifier>>


==== Semantics

. The identifier is the name of the state.

. the state definition member sequence _S_ contains any state definitions,
then it must contain exactly one initial transition specifier _I_.
The state definition or junction definition referred to in _I_
must be an element of _S_.
_I_ specifies the sub-state to enter on entry to the outer state.

==== Examples

[source,fpp]
----
state machine MonitorSm {

  event Complete
  event Drive
  event Calibrate
  event RTI
  event Stop
  event Fault
  
  action init2
  action doCalibrate
  action motorControl
  action reportFault

  guard calibrateReady

  initial DEVICE_ON
  
  state DEVICE_ON {

    initial INITIALIZING do init2

    state INITIALIZING {
      on Complete visit IDLE
    }

    state IDLE {
      on Drive visit DRIVING
      on Calibrate if calibrateReady visit CALIBRATING
    }

    state CALIBRATING {
      on RTI do doCalibrate
      on Fault visit Idle do reportFault
      on Complete visit IDLE
    }

    state DRIVING {
      on RTI do motorControl
      on Stop visit IDLE
    }

  }

}
----
